set hive.execution.engine=tez;

!echo Parms to Hive Script: filename='${hiveconf:file_name}';

create external table if not exists ext_cdrdm.SUBSCRIBER_STORE(
EVENT_TIME      STRING,
NME_CLASS       STRING,
NME_TYPE        STRING,
CTN     STRING,
BAN     STRING,
SUBSCRIBER_TYPE STRING,
TX_TYPE STRING,
SESSION_ID      STRING,
TRANSACTION_TYPE STRING,
OPERATION STRING,
CHANNEL_ID STRING,
USER_SUBSCRIPTION STRING,
RLH_ELIGIBLE STRING,
RLH_OPTIN STRING,
LASTUSER_SUBSCRIPTION STRING,
OLD_RLH_ELIGIBLE STRING,
OLD_RLH_OPTIN STRING,
RESULT STRING
)
ROW FORMAT SERDE 'org.apache.hadoop.hive.serde2.OpenCSVSerde'
WITH SERDEPROPERTIES ('separatorChar' ='\,')
STORED AS TEXTFILE
LOCATION '${hiveconf:file_name}'
tblproperties("skip.header.line.count"="1");

create table if not exists CDRDM.SUBSCRIBER_STORE
(
EVENT_TIME      STRING,
NME_CLASS       STRING,
NME_TYPE        STRING,
CTN     STRING,
BAN     STRING,
SUBSCRIBER_TYPE STRING,
TX_TYPE STRING,
SESSION_ID      STRING,
TRANSACTION_TYPE STRING,
OPERATION STRING,
CHANNEL_ID STRING,
USER_SUBSCRIPTION STRING,
RLH_ELIGIBLE STRING,
RLH_OPTIN STRING,
LASTUSER_SUBSCRIPTION STRING,
OLD_RLH_ELIGIBLE STRING,
OLD_RLH_OPTIN STRING,
RESULT STRING,
FILE_NAME    STRING,
HDP_CREATED_TS TIMESTAMP
)
partitioned by (EVENT_DATE DATE)
stored as ORC;

!echo loading the table CDRDM.SUBSCRIBER_STORE;

INSERT INTO TABLE CDRDM.SUBSCRIBER_STORE PARTITION(EVENT_DATE)
SELECT
EVENT_TIME,
NME_CLASS ,
NME_TYPE  ,
CTN,
BAN,
SUBSCRIBER_TYPE,
TX_TYPE,
SESSION_ID,
TRANSACTION_TYPE,
OPERATION,
CHANNEL_ID,
USER_SUBSCRIPTION,
RLH_ELIGIBLE,
RLH_OPTIN,
LASTUSER_SUBSCRIPTION,
OLD_RLH_ELIGIBLE,
OLD_RLH_OPTIN,
RESULT,
trim(regexp_extract(input__file__name,'[^/]+$', 0)),
current_timestamp,
cast(substring(from_unixtime(unix_timestamp(EVENT_TIME,'MM/dd/yyyy')),1,10) as date) as EVENT_DATE
FROM
ext_cdrdm.SUBSCRIBER_STORE;
