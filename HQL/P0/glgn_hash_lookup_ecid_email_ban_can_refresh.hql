set hive.execution.engine=mr;
set hive.support.quoted.identifiers=none;
set hive.optimize.sort.dynamic.partition=false;
set hive.exec.dynamic.partition.mode=nonstrict;
set hive.exec.dynamic.partition=true;
set mapreduce.map.memory.mb=9000;
set mapreduce.map.java.opts=-Xmx8500m;

use hash_lookup;
DROP TABLE IF EXISTS ecid_email_ban_can_prev;
DROP TABLE IF EXISTS ecid_email_ban_can_new;

CREATE TABLE IF NOT EXISTS ecid_email_ban_can
(ACCOUNT VARCHAR(40),
HASH_ACCOUNT VARCHAR(64),
ECID BIGINT,
CRM_EMAIL VARCHAR(250),
HASH_CRM_EMAIL VARCHAR(64),
BAN VARCHAR(40),
HASH_BAN VARCHAR(64),
DOTCOM_EMAIL VARCHAR(250),
HASH_DOTCOM_EMAIL VARCHAR(64),
SYS_SRC_CD VARCHAR(20)
)
STORED AS ORC
TBLPROPERTIES (
  'orc.row.index.stride'='50000',
  'orc.stripe.size'='536870912');

 CREATE TABLE IF NOT EXISTS ecid_email_ban_can_new
(ACCOUNT VARCHAR(40),
HASH_ACCOUNT VARCHAR(64),
ECID BIGINT,
CRM_EMAIL VARCHAR(250),
HASH_CRM_EMAIL VARCHAR(64),
BAN VARCHAR(40),
HASH_BAN VARCHAR(64),
DOTCOM_EMAIL VARCHAR(250),
HASH_DOTCOM_EMAIL VARCHAR(64),
SYS_SRC_CD VARCHAR(20)
)
STORED AS ORC
TBLPROPERTIES (
  'orc.row.index.stride'='50000',
  'orc.stripe.size'='536870912');
  
INSERT OVERWRITE TABLE ecid_email_ban_can_new
SELECT ACCT_KEY,
CASE WHEN ACCT_KEY IS NULL THEN NULL ELSE reflect('org.apache.commons.codec.digest.DigestUtils', 'sha256Hex',CAST(ACCT_KEY AS STRING)) END,
ECID,
CRM_EMAIL,
CASE WHEN CRM_EMAIL IS NULL THEN NULL ELSE reflect('org.apache.commons.codec.digest.DigestUtils', 'sha256Hex',CAST(CRM_EMAIL AS STRING)) END,
BAN,
CASE WHEN BAN IS NULL THEN NULL ELSE reflect('org.apache.commons.codec.digest.DigestUtils', 'sha256Hex',CAST(BAN AS STRING)) END,
DOTCOM_EMAIL,
CASE WHEN DOTCOM_EMAIL IS NULL THEN NULL ELSE reflect('org.apache.commons.codec.digest.DigestUtils', 'sha256Hex',CAST(DOTCOM_EMAIL AS STRING)) END,
SYSTEM_SRC_CD
FROM (
SELECT DISTINCT CASE WHEN CRM.SRC_ACCT_KEY IS NULL THEN DTCOM.ACCTNBR ELSE CRM.SRC_ACCT_KEY END AS ACCT_KEY,
CASE WHEN CRM.ENTERPRISE_ID IS NULL THEN DTCOM.ENTERPRISE_ID ELSE CRM.ENTERPRISE_ID END AS ECID,
LOWER(CRM.E_MAIL) AS CRM_EMAIL,
DTCOM.ACCTNBR AS BAN,
LOWER(DTCOM.ACCTEMAIL) AS DOTCOM_EMAIL,
CRM.SYSTEM_SRC_CD
FROM (
(SELECT DISTINCT A.E_MAIL , B.ENTERPRISE_ID , C.SRC_ACCT_KEY , C.SYSTEM_SRC_CD
FROM ELA_CRM.TABLE_CONTACT A JOIN ELA_RCIS.EC_CUST_ACCT B
ON A.X_RCIS_ID = CAST(B.ENTERPRISE_ID AS string)
JOIN ELA_RCIS.EC_ACCOUNT C
ON B.ACCT_ID = C.ACCT_ID
WHERE  (B.EXPIRY_DT IS NULL  OR B.EXPIRY_DT = 'NULL')) CRM
FULL OUTER JOIN
(SELECT  DISTINCT E.ACCTNBR , E.ACCTEMAIL , G.ENTERPRISE_ID
FROM oth_source.acctassoc E JOIN ELA_RCIS.EC_ACCOUNT F
ON E.ACCTNBR = F.SRC_ACCT_KEY
JOIN ELA_RCIS.EC_CUST_ACCT G
ON F.ACCT_ID = G.ACCT_ID
WHERE (G.EXPIRY_DT IS NULL  OR G.EXPIRY_DT = 'NULL')) DTCOM
ON CRM.SRC_ACCT_KEY = DTCOM.ACCTNBR)) Z;

ALTER TABLE ecid_email_ban_can RENAME TO ecid_email_ban_can_prev;
ALTER TABLE ecid_email_ban_can_new RENAME TO ecid_email_ban_can;

DROP TABLE IF EXISTS ecid_email_ban_can_prev;
DROP TABLE IF EXISTS ecid_email_ban_can_new;
