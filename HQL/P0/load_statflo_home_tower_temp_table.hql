set hive.tez.container.size=8192;

INSERT OVERWRITE TABLE NW_TOWER_USAGE.STATFLO_HOME_TOWER_TEMP
SELECT SUBSCRIBER_NO,ERP_SITE_LOCATION_CODE,ERP_SITE_NAME,CELLMAPNAME,X,Y,ADDRESS,CITY,PROVINCE,HOME_SECTOR_CNT
FROM
  (SELECT *,
    ROW_NUMBER() OVER(PARTITION BY SUBSCRIBER_NO ORDER BY HOME_SECTOR_CNT DESC,X) AS HOME_SECTOR_RNK
  FROM
    (SELECT SUBSCRIBER_NO,
      ERP_SITE_LOCATION_CODE,
      ERP_SITE_NAME,
      CELLMAPNAME,
      X,
      Y,
      ADDRESS,
      CITY,
      PROVINCE,
      COUNT(*) AS HOME_SECTOR_CNT
    FROM
      (SELECT *
      FROM
        (SELECT *
        FROM
          (SELECT RECORD_OPENING_DATE,
            SUBSCRIBER_NO,
            EUTRAN_CELLID,
            RECORD_OPENING_TIME,
            PLMN_ID,
            TRACKING_AREA_CODE,
            ROW_NUMBER() OVER(PARTITION BY SUBSCRIBER_NO,RECORD_OPENING_DATE ORDER BY RECORD_OPENING_TIME DESC) AS LAST_TOWER_B4_SLEEP
          FROM CDRDM.FACT_GPRS_CDR
          WHERE RECORD_OPENING_DATE BETWEEN ADD_MONTHS(CURRENT_DATE,-1) AND CURRENT_DATE
          AND GPRS_CHOICE_MASK_ARCHIVE = 21
          AND EUTRAN_CELLID           IS NOT NULL
          AND TRIM(EUTRAN_CELLID)     <> '00000000'
          ) AS AA
        WHERE AA.LAST_TOWER_B4_SLEEP = 1
        ) AS AA
      LEFT JOIN
        (SELECT CGI_HEX,
          CGI,
          LOCATIONCO AS ERP_SITE_LOCATION_CODE,
          SITE_NAME  AS ERP_SITE_NAME,
          CELL       AS CELLMAPNAME,
          X,
          Y,
          ADDRESS,
          CITY,
          PROVINCE
        FROM
          (SELECT *,
            ROW_NUMBER() OVER(PARTITION BY CGI ORDER BY INSERT_TIMESTAMP DESC) AS LATEST
          FROM CDRDM.DIM_CELL_SITE_INFO
          ) AS AA
        WHERE AA.LATEST=1
        AND CGI       IS NOT NULL
        ) AS BB
      ON CONCAT(SUBSTR(AA.PLMN_ID,0,3),'-',SUBSTR(AA.PLMN_ID,-3),'-',CONV(AA.TRACKING_AREA_CODE,16,10),'-',CONV(AA.EUTRAN_CELLID,16,10)) = TRIM(BB.CGI)
      ) AS RLLUP
    GROUP BY SUBSCRIBER_NO,
      ERP_SITE_LOCATION_CODE,
      ERP_SITE_NAME,
      CELLMAPNAME,
      X,
      Y,
      ADDRESS,
      CITY,
      PROVINCE
    ORDER BY HOME_SECTOR_CNT
    ) AS FNL_ROLLUP
  ) AS FNL_RUP
WHERE FNL_RUP.HOME_SECTOR_RNK = 1 ;

