set hive.tez.container.size=8192;

INSERT OVERWRITE TABLE NW_TOWER_USAGE.STATFLO_WB_DISTANCE_TEMP_OR
SELECT DISTANCE.ECID, DISTANCE.BAN,DISTANCE.SUBSCRIBER_NO,DISTANCE.FRANCHISE_TP,DISTANCE.CTN_LAT,DISTANCE.CTN_LON
FROM
	(SELECT A.ECID,A.BAN,A.SUBSCRIBER_NO,A.FRANCHISE_TP,B.Y AS CTN_LAT,B.X AS CTN_LON
	FROM NW_TOWER_USAGE.STATFLO_WB_CUST_TEMP A
	JOIN (SELECT NVL(C.SUBSCRIBER_NO,D.SUBSCRIBER_NO) AS SUBSCRIBER_NO,NVL(C.Y,D.Y) AS Y,NVL(C.X,D.X) X
      FROM (SELECT SUBSCRIBER_NO,X,Y FROM NW_TOWER_USAGE.STATFLO_WB_HOME_TOWER_TEMP
            WHERE HOME_SECTOR_CNT >= 8) C
      FULL OUTER JOIN 
           (SELECT SUBSCRIBER_NO,X,Y FROM NW_TOWER_USAGE.STATFLO_CHATR_HOME_TEMP
            WHERE DAYS_USAGE_COUNT >= 8) D
      ON C.SUBSCRIBER_NO = D.SUBSCRIBER_NO) B
	ON A.SUBSCRIBER_NO = B.SUBSCRIBER_NO
	) DISTANCE
LEFT JOIN NW_TOWER_USAGE.STATFLO_WB_OUTPUT_AND ANDLOGIC
ON DISTANCE.BAN = ANDLOGIC.BAN
AND DISTANCE.SUBSCRIBER_NO = ANDLOGIC.SUBSCRIBER_NO
WHERE ANDLOGIC.SUBSCRIBER_NO IS NULL;

INSERT OVERWRITE TABLE NW_TOWER_USAGE.STATFLO_WB_DISTANCE_OR
SELECT STORE_ID,CHAIN_CODE,LOCATION_ID,BANNER,LOCATION_NAME,CMA,ECID,BAN,SUBSCRIBER_NO,DISTANCECALC,LOCATION_TYPE,SQ_FT
FROM NW_TOWER_USAGE.STATFLO_WB_DISTANCE_TEMP_OR BASE
JOIN
  (SELECT B.STORE_ID,B.CHAIN_CODE,B.LOCATION_ID,B.BANNER,B.LOCATION_NAME,B.CMA,A.CTN_LAT,A.CTN_LON,
    60*1.1515*1.609*(180*(ACOS(((SIN(RADIANS(B.LATITUDE))*SIN(RADIANS(A.CTN_LAT))) + (COS(RADIANS(B.LATITUDE))*COS(RADIANS(A.CTN_LAT))*COS(RADIANS(B.LONGITUDE-A.CTN_LON))))))/PI()) AS DISTANCECALC,LOCATION_TYPE,SQ_FT
  FROM
    (SELECT CTN_LAT,CTN_LON
    FROM NW_TOWER_USAGE.STATFLO_WB_DISTANCE_TEMP_OR
    GROUP BY CTN_LAT,CTN_LON
    )A
  JOIN
    (SELECT BRSC_ID AS STORE_ID,CHAINOFSTORESCODE AS CHAIN_CODE, LOCATION_ID,
      CASE WHEN FRANCHISE = 'R' THEN 'Rogers' WHEN FRANCHISE = 'F' THEN 'Fido' END AS BANNER,
      LOCATION_NAME,CMA,LATITUDE,LONGITUDE,LOCATION_TYPE,SQ_FT
    FROM ODS_BMRT.DIM_STORE_DEALER_CODE
    WHERE FRANCHISE        IN ('R','F')
    AND UPPER(STORE_STATUS) = 'OPEN'
    AND RETAIL_STORE        = 'Y'
    AND STORE_DESIGN_FT != 'sub-agent'
    AND LOCATION_NAME NOT LIKE '%HEAD OFFICE%'
	AND UPPER(LOCATION_TYPE) != UPPER('Cable Counter')
    ) B
  ) QRY ON BASE.CTN_LAT = QRY.CTN_LAT
AND BASE.CTN_LON        = QRY.CTN_LON
WHERE DISTANCECALC     <= 25
AND CASE WHEN FRANCHISE_TP = 'R' THEN 'Rogers' WHEN FRANCHISE_TP = 'F' THEN 'Fido' END = BANNER;


